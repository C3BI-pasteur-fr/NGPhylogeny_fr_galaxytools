
nstates PROT;
mxram 4000;
proc data.tnt;

sect: slack 1000 ;

macro=; 
lquote [ ; 

log tnt_analysis.out; 

watch=; 

coll tbr ;

drift : fitd 2 rfitd 0.1 ; 

var :
  i j
  plotapo bootit
  runquickie dobremer
  medium big verybig 
;

set medium 200 ;
set big 500 ; 
set verybig 1500 ;
set plotapo 0 ;
set runquickie 0 ; 
set dobremer 0 ;
set bootit 0 ; 

if ( ntax > 'medium' ) 
if ( ntax < 'verybig' ) 
quote 
****************************************************
 This is a large data set.  A consensus estimation 
 instead of a complete search is recommended. 
****************************************************
; 
else 
quote 
*************************************************************
 This is a very large data set.  A consensus estimation 
 instead of a complete search is VERY STRONGLY RECOMMENDED 
*************************************************************
;
end
end
 
if ( ntax <= 'medium' ) 
quote 
This is a reasonnable dataset, complete search. 
;
else 
quote 
This is a large dataset, consensus estimatation instead of a complete search. 
;
end 

set runquickie = (ntax <= 'medium') ;

resettime ;

if ( ( ntax > 'medium' ) && 'runquickie' ) 
  set 0 ntax / 70 ; 
  set 1 'i' / 2 ; 
  if ( 'i' < 2 ) set i 2 ; end 
  if ( 'j' < 3 ) set j 3 ; end 
  if ( 'j' > 20 ) set j 20 ; end 
  if ( 'i' > 30 ) set i 30 ; end  
  sec: xss'/.0i'-'/.0j'+3-1 gocomb 60 fuse 4 drift 5 combstart 5 ;
  if ( ntax < 'big' )
       quote
        ------------------------------------------------------------
        Search routine used: 
           a quick consensus estimation (Goloboff &38 Farris 2001),
           with 15 replications (each with default &34xmult&34  but
           with 3 starting points instead of the default 5, and using 
           XSS --see Goloboff &38 Pol 2007).  The sectorial searches
           analyzing sectors of 60 or more taxa with a combined 
           strategy (5 starting points, 5 cycles of tree-drifting for
           each, fusing the results in 4 cycles).  Sectors selections:
           XSS dividing tree in '/.0i' to '/.0j' parts, 3 times., CSS
           and RSS with defaults.  For more details of CSS, RSS, 
           and tree-drifting, see Goloboff 1999., for details of
           XSS, see Goloboff &38 Pol 2007.  For details of &34xmult&34,
           see documentation of TNT.
           Note: for consensus calculation, trees TBR-collapsed. 
        ------------------------------------------------------------ ; 
       qnelsen [ xmu = rep 3 xss ; ] ;
  else
   if ( ntax < 'verybig' ) 
       quote
        ------------------------------------------------------------
        Search routine used: 
           a quick consensus estimation (Goloboff &38 Farris 2001),
           with 15 random addition sequences, plus TBR, and sectorial 
           searches using CSS (defaults), RSS (defaults), and 
           XSS (dividing tree in '/.0i' to '/.0j' parts, 3 times),
           analyzing sectors of 60 or more taxa with a combined 
           strategy (5 starting points, 5 cycles of tree-drifting for
           each, fusing the results in 4 cycles).  For details of 
           CSS, RSS, and tree-drifting, see Goloboff 1999., for details 
           of XSS, see Goloboff &38  Pol 2007.   For details of &34xmult&34,
           see documentation of TNT.
           Note: for consensus calculation, trees TBR-collapsed. 
        ------------------------------------------------------------ ; 
       qnelsen [ mu1=ho1 ; sec=xss ; drift=iter5; ] ;
   else 
       quote
        ------------------------------------------------------------
        Search routine used: 
           a quick consensus estimation (Goloboff &38 Farris 2001),
           with 15 random addition sequences plus TBR.
           Note: for consensus calculation, trees TBR-collapsed. 
        ------------------------------------------------------------ ; 
       qnelsen [ mu1=ho1 ; ] ;
    end 
  end 
else 
  report +/1 ; 
       quote
        ------------------------------------------------------------
        Search routine used: 
           finding optimal score 20 times independently, using defaults
           of &34xmult&34  plus 10 cycles of tree-drifting (Goloboff
           1999).
           Note: for consensus calculation, trees TBR-collapsed. 
           ALSO NOTE: THIS IS AN OVERKILL FOR MOST DATA SETS!
        ------------------------------------------------------------ ; 
  hold 1000 ; 
  xmu = hit 20 drift 10 ;
  nelsen * ; 
  tchoose { strict } ; 
end

export - tnt_trees.tre ;
quote Results saved (as text) to file tnt_results.out &10&10; 

log / ; 
set i time ; 
quote 
***************************************************
ANALYSIS COMPLETED (total time used: '/.0i' sec) 
***************************************************
               Please note!!

    ...for each of the quantities calculated 
    by this script, it is possible to use more
    exhaustive and accurate (but more time
    consuming) algorithms, or viceversa. The
    routines used by the script are only an
    approximation which provides a good tradeoff
    between accuracy and run-times for most data
    sets
;

proc/;

